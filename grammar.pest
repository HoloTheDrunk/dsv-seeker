WHITESPACE = _{ " " | "\t" | NEWLINE }
ws = _{ WHITESPACE }

quoted = @{ (!"\'" ~ ANY)* }
string = _{ "\'" ~ quoted ~ "\'" }

ident = { (ASCII_ALPHA | "_")+ }

atom = _{ ident | string }

select = ${ "select" ~ ws+ ~ (
    	(atom ~ (ws* ~ "," ~ ws* ~ atom)*) 
      	| "*"
    )}

comparison = ${ "where" ~ ws+ ~ atom ~ ws* ~ comparator ~ ws* ~ atom }
comparator = { "=" | "like" }

enumerate = ${ "enum" ~ ws+ ~ atom }

sort = ${ "sort" ~ ws+ ~ (numerical_sort | atom) ~ ws+ ~ sort_direction }
numerical_sort = ${ "num" ~ ws+ ~ atom }
sort_direction = ${ "asc" | "desc" | "posc" }

command = _{ select | comparison | enumerate | sort }

program = _{ SOI ~ (command ~ ("|" ~ command)*)? ~ EOI }
